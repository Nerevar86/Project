
#Область ОписаниеПеременных

&НаКлиенте
Перем РазрешеноЗакрытиеФормы;

&НаКлиенте
Перем ШаблонТекстаОшибкиЗаполнения;

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ЗагрузитьНастройки();
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если Не ЗавершениеРаботы Тогда
		Отказ = Не РазрешеноЗакрытиеФормы;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ApiKeyПриИзменении(Элемент)
	ОбработатьЗначениеЭлементаФормы(Элемент);
КонецПроцедуры

&НаКлиенте
Процедура ClientIDПриИзменении(Элемент)
	ОбработатьЗначениеЭлементаФормы(Элемент);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормы
// Код процедур и функций
#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Команда1(Команда)
	Команда1НаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ПодтвердитьИЗакрыть(Команда)
	
	СохранитьНастройки();
	
	РазрешеноЗакрытиеФормы = Истина;
	
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура Подтвердить(Команда)
	
	СохранитьНастройки();
	
	ОчиститьСообщения();
	
	ОбщегоНазначенияКлиент.СообщитьПользователю(Нстр("ru = 'Настройки записаны'"));
	
КонецПроцедуры

&НаКлиенте
Процедура ЗакрытьФорму(Команда)
	
	РазрешеноЗакрытиеФормы = Истина;
	
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьДанныеOzon(Команда)
	
	Поля = Новый Массив;
	Поля.Добавить(Элементы.ClientID.Имя);
	Поля.Добавить(Элементы.ApiKey.Имя);
	
	ОшибкиЗаполнения = РезультатПроверкиЗаполненностиПолейФормы(Поля);
	Если ОшибкиЗаполнения.Количество() Тогда
		Для Каждого Параметр Из ОшибкиЗаполнения Цикл
			ОбщегоНазначенияКлиент.СообщитьПользователю(Параметр.Значение,, Параметр.Ключ);
		КонецЦикла;
		Возврат;
	КонецЕсли;
	
	ПараметрыЗапроса = Новый Структура;
	ПараметрыЗапроса.Вставить(Элементы.ClientID.Имя, ClientID);
	ПараметрыЗапроса.Вставить(Элементы.ApiKey.Имя, ApiKey);
	
	Если СписокСкладов.Количество() Или СписокТоваров.Количество() Тогда
		ТекстВопроса = Нстр("ru = 'В табличных частях имеются заполненные соответствия OZON,
		|текущие данные будут очищены и перезаполнены. Продолжить?'");
		Обработчик = Новый ОписаниеОповещения("ЗаполнитьДанныеOzonЗавершение", ЭтотОбъект, ПараметрыЗапроса);
		ПоказатьВопрос(Обработчик, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
	Иначе
		РезультатОтвета = КодВозвратаДиалога.Да;
		ЗаполнитьДанныеOzonЗавершение(РезультатОтвета, ПараметрыЗапроса);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьОстаткиНаСкладеOzon(Команда)
	
	ОчиститьСообщения();
	
	Отказ = Ложь;
	
	Поля = Новый Массив;
	Поля.Добавить(Элементы.ClientID.Имя);
	Поля.Добавить(Элементы.ApiKey.Имя);
	
	ОшибкиЗаполнения = РезультатПроверкиЗаполненностиПолейФормы(Поля);
	Если ОшибкиЗаполнения.Количество() Тогда
		Для Каждого Параметр Из ОшибкиЗаполнения Цикл
			ОбщегоНазначенияКлиент.СообщитьПользователю(Параметр.Значение,, Параметр.Ключ,, Отказ);
		КонецЦикла;
	КонецЕсли;
	
	Если Не Отказ Тогда
		
		ИдентификаторыВыбранныхСтрок = Новый Массив;
		
		Если Не СписокСкладов.Количество() Тогда
			Текст = Нстр("ru = 'Не заполнены данные склада Оzon.'");
			ОбщегоНазначенияКлиент.СообщитьПользователю(Текст,,,, Отказ);
		Иначе
			СтрокаСклада = СписокСкладов[0];
			Если Не ЗначениеЗаполнено(СтрокаСклада.warehouse_1s) Тогда
				Текст = СтрШаблон(Нстр("ru = 'Не выбрано соответствие для склада Оzon: %1'"),
					СтрокаСклада.name);
				ОбщегоНазначенияКлиент.СообщитьПользователю(Текст,,"СписокСкладов[0].warehouse_1s",, Отказ);
			КонецЕсли;
		КонецЕсли;
		
		Если Не СписокТоваров.Количество() Тогда
			Текст = Нстр("ru = 'Не заполнены данные товарных позиций Ozon.'");
			ОбщегоНазначенияКлиент.СообщитьПользователю(Текст,,,, Отказ);
		Иначе
			ПараметрыОтбора = Новый Структура;
			ПараметрыОтбора.Вставить("mark", Истина);
			
			НайденныеСтроки = СписокТоваров.НайтиСтроки(ПараметрыОтбора);
			Если Не НайденныеСтроки.Количество() Тогда
				Текст = Нстр("ru = 'Отсутствуют выбранные товарные позиции Ozon для обновления.'");
				ОбщегоНазначенияКлиент.СообщитьПользователю(Текст,,,, Отказ);
			Иначе
				Для Каждого НайденнаяСтрока Из НайденныеСтроки Цикл
					ИдентификаторыВыбранныхСтрок.Добавить(НайденнаяСтрока.ПолучитьИдентификатор());
				КонецЦикла;
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
	Если Не Отказ Тогда
		
		РезультатВыполнения = ОбновитьОстаткиНаСкладеOzonНаСервере(ИдентификаторыВыбранныхСтрок);
		Если РезультатВыполнения = Неопределено Тогда
			Текст = Нстр("ru = 'Не удалось получить данные по остаткам выбранных товарных позиций в базе.'");
			ОбщегоНазначенияКлиент.СообщитьПользователю(Текст);
		Иначе
			Попытка
				Для Каждого СтрокаРезультата Из РезультатВыполнения.Ответ.result Цикл
					Если СтрокаРезультата.errors.Количество() > 0 Тогда
						Текст = СтрШаблон(
						Нстр("ru = 'Возникла ошибка при обновлении остатков товарной позиции: %1
							|Причина: {%2}'"),
							СтрокаРезультата.offer_id, СтрокаРезультата.errors[0].message);
					Иначе
						Текст = СтрШаблон(
						Нстр("ru = 'Остатки товарной позиции: %1 успешно обновлены'"),
						СтрокаРезультата.offer_id);
					КонецЕсли;
					ОбщегоНазначенияКлиент.СообщитьПользователю(Текст);
				КонецЦикла;
			Исключение
				Текст = КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
				ОбщегоНазначенияКлиент.СообщитьПользователю(Текст);
			КонецПопытки;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура СохранитьНастройки()
	
	КлючОбъекта = МодульСервер().КлючНастроек();
	
	ПараметрыСохранения = Новый Структура;
	ПараметрыСохранения.Вставить(Элементы.ClientID.Имя, ClientID);
	ПараметрыСохранения.Вставить(Элементы.ApiKey.Имя, ApiKey);
	ПараметрыСохранения.Вставить(Элементы.СписокСкладов.Имя, СписокСкладов.Выгрузить());
	ПараметрыСохранения.Вставить(Элементы.СписокТоваров.Имя, СписокТоваров.Выгрузить());
	
	ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(КлючОбъекта, Неопределено, ПараметрыСохранения);
	
КонецПроцедуры

&НаСервере
Процедура ЗагрузитьНастройки()
	
	МодульСервер = МодульСервер();
	
	ЗагруженныеНастройки = МодульСервер.НастройкиOzon();
	Если ЗагруженныеНастройки <> Неопределено Тогда
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, ЗагруженныеНастройки,, "СписокСкладов,СписокТоваров");
		Если ЗагруженныеНастройки.Свойство(Элементы.СписокСкладов.Имя) Тогда
			ЭтотОбъект[Элементы.СписокСкладов.Имя].Загрузить(ЗагруженныеНастройки[Элементы.СписокСкладов.Имя]);
		КонецЕсли;
		Если ЗагруженныеНастройки.Свойство(Элементы.СписокТоваров.Имя) Тогда
			ЭтотОбъект[Элементы.СписокТоваров.Имя].Загрузить(ЗагруженныеНастройки[Элементы.СписокТоваров.Имя]);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция СписокСкладовНаСервере(ПараметрыЗапроса)
	Возврат МодульСервер().СписокСкладовOzon(ПараметрыЗапроса);
КонецФункции

&НаСервере
Функция МодульСервер()
	Возврат РеквизитФормыВЗначение("Объект");
КонецФункции

&НаСервере
Процедура ЗаполнитьСписокТоваров(ПараметрыЗапроса)
	Результат = МодульСервер().СписокТоваровOzon(ПараметрыЗапроса);;
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДанныеOzonНаСервере(ПараметрыЗапроса)
	
	МодульСервер = МодульСервер();
	
	СписокСкладовТЗ = СписокСкладов.Выгрузить();
	СписокСкладовТЗ.Очистить();

	Результат = МодульСервер.СписокСкладовOzon(ПараметрыЗапроса);
	Если Результат.Ошибка Тогда
		ОбщегоНазначения.СообщитьПользователю(Результат.Ответ.message);
		Возврат;
	КонецЕсли;
	
	ДанныеСкладовOzon = Результат.Ответ.result;
	Для Каждого СтрокаДанныхСклада Из ДанныеСкладовOzon Цикл
		НоваяСтрока = СписокСкладовТЗ.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаДанныхСклада);
	КонецЦикла;
	
	СписокСкладовТЗ.Сортировать("name ВОЗР");
	
	СписокСкладов.Загрузить(СписокСкладовТЗ);
	
	НомерСтроки = 1;
	Для Каждого Строка Из СписокСкладов Цикл
		Строка.number_line = НомерСтроки;
		НомерСтроки = НомерСтроки + 1;
	КонецЦикла;
	
	СписокТоваровТЗ = СписокТоваров.Выгрузить();
	СписокТоваровТЗ.Очистить();
	
	Результат = МодульСервер.СписокТоваровOzon(ПараметрыЗапроса);
	Если Результат.Ошибка Тогда
		ОбщегоНазначения.СообщитьПользователю(Результат.Ответ.message);
		Возврат;
	КонецЕсли;
	
	ДанныеТоваровOzon = Результат.Ответ.result.items;
	Для Каждого СтрокаДанныхТовара Из ДанныеТоваровOzon Цикл
		НоваяСтрока = СписокТоваровТЗ.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаДанныхТовара);
		НоваяСтрока.number_line = НомерСтроки;
		НомерСтроки = НомерСтроки + 1;
	КонецЦикла;
	
	СписокТоваровТЗ.Сортировать("offer_id ВОЗР");
	
	СписокТоваров.Загрузить(СписокТоваровТЗ);
	
	НомерСтроки = 1;
	Для Каждого Строка Из СписокТоваров Цикл
		Строка.number_line = НомерСтроки;
		НомерСтроки = НомерСтроки + 1;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьЗначениеЭлементаФормы(Элемент)
	
	ПодстрокаПоиска = " ";
	
	ПодстрокаЗамены = "";
	
	Если СтрНайти(ЭтотОбъект[Элемент.Имя], ПодстрокаПоиска) > 0 Тогда
		ЭтотОбъект[Элемент.Имя] = СтрЗаменить(ЭтотОбъект[Элемент.Имя], ПодстрокаПоиска, ПодстрокаЗамены);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция РезультатПроверкиЗаполненностиПолейФормы(Поля)
	
	Ошибки = Новый Структура;
	Для Каждого Поле Из Поля Цикл
		Если Не ЗначениеЗаполнено(ЭтотОбъект[Поле]) Тогда
			Текст = СтрШаблон(Нстр(ШаблонТекстаОшибкиЗаполнения), Поле);
			Ошибки.Вставить(Поле, Текст);
		КонецЕсли;
	КонецЦикла;
	
	Возврат Ошибки;
	
КонецФункции

&НаСервере
Функция ОбновитьОстаткиНаСкладеOzonНаСервере(ИдентификаторыСтрок)
	
	РезультатВыполнения = Неопределено;
	
	МодульСервер = МодульСервер();
	
	СтрокиТоваров = Новый Массив;
	Для Каждого ИдентификаторСтроки Из ИдентификаторыСтрок Цикл
		СтрокаТоваров = СписокТоваров.НайтиПоИдентификатору(ИдентификаторСтроки);
		Если СтрокаТоваров <> Неопределено Тогда
			СтрокиТоваров.Добавить(СтрокаТоваров);
		КонецЕсли;
	КонецЦикла;
	
	ОстаткиТоваров = МодульСервер.ОстаткиТоваровДляЗапросаOzon(
		СписокТоваров.Выгрузить(СтрокиТоваров), СписокСкладов[0]);
	Если Не ОстаткиТоваров.Количество() Тогда
		Возврат РезультатВыполнения;
	КонецЕсли;
	
	ПараметрыЗапроса = Новый Структура;
	ПараметрыЗапроса.Вставить(Элементы.ClientID.Имя, ClientID);
	ПараметрыЗапроса.Вставить(Элементы.ApiKey.Имя, ApiKey);
	
	ТелоЗапроса = Новый Структура;
	ТелоЗапроса.Вставить("stocks", ОстаткиТоваров);
	
	ПараметрыЗапроса.Вставить("ТелоЗапроса", ТелоЗапроса);
	
	РезультатВыполнения = МодульСервер.ОбновитьОстаткиOzon(ПараметрыЗапроса);
	
	Возврат РезультатВыполнения;
	
КонецФункции

&НаСервере
Процедура Команда1НаСервере()
	МодульСервер = МодульСервер();
	МодульСервер.ВыполнитьКоманду(МодульСервер.ИдентификаторКомандыОбновленияОстатковOzonПоРасписанию());
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиОповещения

&НаКлиенте
Процедура ЗаполнитьДанныеOzonЗавершение(Результат, ПараметрыЗапроса) Экспорт
	Если Результат = КодВозвратаДиалога.Да Тогда
		ЗаполнитьДанныеOzonНаСервере(ПараметрыЗапроса)
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокТоваровcharact_1sНачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	
	//СтандартнаяОбработка = Ложь;
	//
	//Отбор = Новый Структура;
	//Отбор.Вставить("Владелец", Элемент.Родитель.ТекущиеДанные.product_1s);
	//Отбор.Вставить("ПометкаУдаления", Ложь);
	//
	//ПараметрыФормы = Новый Структура;
	//ПараметрыФормы.Вставить("МножественныйВыбор", Ложь);
	//ПараметрыФормы.Вставить("Отбор", Отбор);
	//
	//ОткрытьФорму("Справочник.ХарактеристикиНоменклатуры.ФормаВыбора",
	//	ПараметрыФормы,
	//	Элемент,,,,,
	//	РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		
КонецПроцедуры

&НаКлиенте
Процедура СписокТоваровproduct_1sПриИзменении(Элемент)
	
	ТекущиеДанные = Элемент.Родитель.ТекущиеДанные;
	Если ЗначениеЗаполнено(ТекущиеДанные.product_1s) Тогда
		ТекущиеДанные.charact_1s_used = ЕстьХарактеристики(ТекущиеДанные.product_1s);
	Иначе
		ТекущиеДанные.charact_1s_used = Ложь;
	КонецЕсли;
	
	УстановитьЗначениеПометкиПриИзмененииДанныхНоменклатуры(ТекущиеДанные);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ЕстьХарактеристики(Владелец)
	
	ЕстьХарактеристики = Ложь;
	
	ЕстьХарактеристики = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Владелец, "ИспользоватьХарактеристики");
	//Если ЕстьХарактеристики Тогда
	//	
	//	Запрос = Новый Запрос;
	//	Запрос.Текст = 
	//	"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
	//	|	ХарактеристикиНоменклатуры.Ссылка КАК Ссылка
	//	|ИЗ
	//	|	Справочник.ХарактеристикиНоменклатуры КАК ХарактеристикиНоменклатуры
	//	|ГДЕ
	//	|	ХарактеристикиНоменклатуры.Владелец = &Владелец
	//	|	И НЕ ХарактеристикиНоменклатуры.Недействителен
	//	|	И НЕ ХарактеристикиНоменклатуры.ПометкаУдаления";
	//	
	//	Запрос.УстановитьПараметр("Владелец", Владелец);
	//	
	//	УстановитьПривилегированныйРежим(Истина);
	//	
	//	РезультатЗапроса = Запрос.Выполнить();
	//	
	//	УстановитьПривилегированныйРежим(Ложь);
	//	
	//	ЕстьХарактеристики = Не РезультатЗапроса.Пустой();
	//	
	//КонецЕсли;
	
	Возврат ЕстьХарактеристики;
	
КонецФункции

&НаКлиенте
Процедура СписокТоваровcharact_1sПриИзменении(Элемент)
	
	УстановитьЗначениеПометкиПриИзмененииДанныхНоменклатуры(Элемент.Родитель.ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьЗначениеПометкиПриИзмененииДанныхНоменклатуры(ТекущиеДанные)
	
	ТекущиеДанные.mark = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокТоваровПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
КонецПроцедуры

&НаКлиенте
Процедура СнятьПометкиТоваров(Команда)
	// Вставить содержимое обработчика.
КонецПроцедуры

&НаКлиенте
Процедура УстановитьПометкиТоваров(Команда)
	// Вставить содержимое обработчика.
КонецПроцедуры

#КонецОбласти

#Область Инициализация

ШаблонТекстаОшибкиЗаполнения = "ru = 'Не заполнено поле настроек: %1'";

РазрешеноЗакрытиеФормы = Ложь;

#КонецОбласти

















