
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
Функция СведенияОВнешнейОбработке(ВерсияБСП = "") Экспорт
	
	ПараметрыРегистрации = ДополнительныеОтчетыИОбработки.СведенияОВнешнейОбработке(ВерсияБСП);
	
	ПараметрыРегистрации.БезопасныйРежим = Ложь;
	ПараметрыРегистрации.Вид = ДополнительныеОтчетыИОбработкиКлиентСервер.ВидОбработкиДополнительнаяОбработка();
	ПараметрыРегистрации.Версия = "1.0.0.1";
	
	НоваяКоманда = ПараметрыРегистрации.Команды.Добавить();
	НоваяКоманда.Представление = "Настройки синхронизации Ozon";
	НоваяКоманда.Идентификатор = "НастройкиСинхронизацииOzon";
	НоваяКоманда.Использование = ДополнительныеОтчетыИОбработкиКлиентСервер.ТипКомандыОткрытиеФормы();
	НоваяКоманда.ПоказыватьОповещение = Ложь;
	
	НоваяКоманда = ПараметрыРегистрации.Команды.Добавить();
	НоваяКоманда.Представление = "Обновить остатки товаров на Ozon (по расписанию)";
	НоваяКоманда.Идентификатор = ИдентификаторКомандыОбновленияОстатковOzonПоРасписанию();
	НоваяКоманда.Использование = ДополнительныеОтчетыИОбработкиКлиентСервер.ТипКомандыВызовСерверногоМетода();
	НоваяКоманда.ПоказыватьОповещение = Ложь;
	
	Возврат ПараметрыРегистрации;
	
КонецФункции


#Область ПрограммныйИнтерфейс

Процедура ВыполнитьКоманду(ИдентификаторКоманды) Экспорт
	
	Если ИдентификаторКоманды = ИдентификаторКомандыОбновленияОстатковOzonПоРасписанию() Тогда
		
		НастройкиOzon = НастройкиOzon();
		Если НастройкиOzon = Неопределено Тогда
			Возврат;
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(НастройкиOzon.ClientID)
			Или Не ЗначениеЗаполнено(НастройкиOzon.ApiKey)
			Или Не НастройкиOzon.СписокТоваров.Количество()
			Или Не НастройкиOzon.СписокСкладов.Количество() Тогда
			Возврат;
		КонецЕсли;
		
		ПараметрыЗапроса = Новый Структура;
		ПараметрыЗапроса.Вставить("ClientID", НастройкиOzon.ClientID);
		ПараметрыЗапроса.Вставить("ApiKey", НастройкиOzon.ApiKey);
		
		ОстаткиТоваров = ОстаткиТоваровДляЗапросаOzon(НастройкиOzon.СписокТоваров, НастройкиOzon.СписокСкладов[0]);
		Если Не ОстаткиТоваров.Количество() Тогда
			Возврат;
		КонецЕсли;
		
		ТелоЗапроса = Новый Структура;
		ТелоЗапроса.Вставить("stocks", ОстаткиТоваров);
		
		ПараметрыЗапроса.Вставить("ТелоЗапроса", ТелоЗапроса);
		
		РезультатВыполнения = ОбновитьОстаткиOzon(ПараметрыЗапроса);
		
	КонецЕсли;
	
КонецПроцедуры

Функция СписокСкладовOzon(ПараметрыЗапроса) Экспорт
	
	Метод = "/v1/warehouse/list";
	
	Тип = "POST";
	
	ТелоЗапроса = Новый Структура;
	ТелоЗапроса.Вставить("filter", 0);
	ТелоЗапроса.Вставить("offset", 0);
	
	ПараметрыЗапроса.Вставить("ТелоЗапроса", ТелоЗапроса);
	
	Возврат РезультатHTTPМетода(Метод, Тип, ПараметрыЗапроса);
	
КонецФункции

Функция СписокТоваровOzon(ПараметрыЗапроса) Экспорт
	
	Метод = "/v3/product/list";
	
	Тип = "POST";
	
	ТелоЗапроса = Новый Структура;
	ТелоЗапроса.Вставить("filter", Новый Структура);
	ТелоЗапроса.Вставить("limit", 1000);
	ТелоЗапроса.Вставить("last_id", "");
	
	ПараметрыЗапроса.Вставить("ТелоЗапроса", ТелоЗапроса);
	
	Возврат РезультатHTTPМетода(Метод, Тип, ПараметрыЗапроса);
	
КонецФункции

Функция ОбновитьОстаткиOzon(ПараметрыЗапроса) Экспорт
	
	Метод = "/v2/products/stocks";
	
	Тип = "POST";
	
	Возврат РезультатHTTPМетода(Метод, Тип, ПараметрыЗапроса);
	
КонецФункции

Функция НастройкиOzon() Экспорт
	
	КлючОбъекта = КлючНастроек();
	
	Возврат ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(КлючОбъекта, Неопределено);
	
КонецФункции

Функция КлючНастроек() Экспорт
	Возврат "MSA_ИнтеграцияOzon";
КонецФункции

Функция ОстаткиТоваровДляЗапросаOzon(СписокТоваров, СтрокаСклада) Экспорт
	
	ОстаткиТоваров = Новый Массив;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Таблица.offer_id КАК offer_id,
	|	Таблица.product_id КАК product_id,
	|	Таблица.product_1s КАК product_1s,
	|	Таблица.charact_1s КАК charact_1s,
	|	Таблица.charact_1s_used КАК charact_1s_used
	|ПОМЕСТИТЬ Номенклатура
	|ИЗ
	|	&Таблица КАК Таблица
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Таблица.offer_id КАК offer_id,
	|	Таблица.product_id КАК product_id,
	|	Таблица.product_1s КАК product_1s,
	|	Таблица.charact_1s КАК charact_1s,
	|	Таблица.charact_1s_used КАК charact_1s_used
	|ПОМЕСТИТЬ НоменклатураОтбор
	|ИЗ
	|	Номенклатура КАК Таблица
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	НоменклатураОтбор.product_id КАК product_id,
	|	СОКРЛП(ВЫРАЗИТЬ(НоменклатураОтбор.offer_id КАК СТРОКА(400))) КАК offer_id,
	|	СУММА(ЕСТЬNULL(ЗапасыОстатки.КоличествоОстаток, 0)) КАК ВНаличии,
	|	СУММА(ЕСТЬNULL(ВЫБОР
	|				КОГДА ЗапасыОстатки.ЗаказПокупателя = ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка)
	|					ТОГДА 0
	|				ИНАЧЕ ЗапасыОстатки.КоличествоОстаток
	|			КОНЕЦ, 0)) КАК Резерв,
	|	СУММА(ЕСТЬNULL(ЗапасыОстатки.КоличествоОстаток - ВЫБОР
	|				КОГДА ЗапасыОстатки.ЗаказПокупателя = ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка)
	|					ТОГДА 0
	|				ИНАЧЕ ЗапасыОстатки.КоличествоОстаток
	|			КОНЕЦ, 0)) КАК Доступно
	|ИЗ
	|	НоменклатураОтбор КАК НоменклатураОтбор
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.Запасы.Остатки(
	|				,
	|				Номенклатура В
	|						(ВЫБРАТЬ
	|							Т.product_1s
	|						ИЗ
	|							НоменклатураОтбор КАК Т)
	|					И СтруктурнаяЕдиница = &СтруктурнаяЕдиница) КАК ЗапасыОстатки
	|		ПО НоменклатураОтбор.product_1s = ЗапасыОстатки.Номенклатура
	|			И (ВЫБОР
	|				КОГДА НоменклатураОтбор.charact_1s_used
	|						И НЕ НоменклатураОтбор.charact_1s = ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)
	|					ТОГДА НоменклатураОтбор.charact_1s = ЗапасыОстатки.Характеристика
	|				ИНАЧЕ ИСТИНА
	|			КОНЕЦ)
	|
	|СГРУППИРОВАТЬ ПО
	|	НоменклатураОтбор.product_id,
	|	СОКРЛП(ВЫРАЗИТЬ(НоменклатураОтбор.offer_id КАК СТРОКА(400)))";
	
	Запрос.УстановитьПараметр("Таблица", СписокТоваров);
	Запрос.УстановитьПараметр("СтруктурнаяЕдиница", СтрокаСклада.warehouse_1s);
	
	УстановитьПривилегированныйРежим(Истина);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	УстановитьПривилегированныйРежим(Ложь);
	
	Если Не РезультатЗапроса.Пустой() Тогда
		
		Выборка = РезультатЗапроса.Выбрать();
		
		Пока Выборка.Следующий() Цикл
			СтрокаОстатков = Новый Структура;
			СтрокаОстатков.Вставить("offer_id", Выборка.offer_id);
			СтрокаОстатков.Вставить("product_id", Выборка.product_id);
			СтрокаОстатков.Вставить("stock", Выборка.Доступно);
			СтрокаОстатков.Вставить("warehouse_id", СтрокаСклада.warehouse_id);
			ОстаткиТоваров.Добавить(СтрокаОстатков);
		КонецЦикла;
		
	КонецЕсли;
	
	Возврат ОстаткиТоваров;
	
КонецФункции

Функция ИдентификаторКомандыОбновленияОстатковOzonПоРасписанию() Экспорт
	Возврат "ОбновитьОстаткиТоваровНаOzonПоРасписанию";
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция РезультатHTTPМетода(Метод, Тип, ПараметрыЗапроса)
	
	Кодировка = "UTF-8";
	
	HTTPСоединение = HTTPСоединениеOzon();
	
	HTTPЗапрос = Новый HTTPЗапрос;
	HTTPЗапрос.АдресРесурса = Метод;
	HTTPЗапрос.Заголовки.Вставить("Client-Id", ПараметрыЗапроса.ClientID);
	HTTPЗапрос.Заголовки.Вставить("Api-Key", ПараметрыЗапроса.ApiKey);
	HTTPЗапрос.Заголовки.Вставить("Content-Type", "application/json");
	
	Если ПараметрыЗапроса.Свойство("ТелоЗапроса") Тогда
		
		ЗаписьJSON = Новый ЗаписьJSON;
		ЗаписьJSON.УстановитьСтроку();
		ЗаписатьJSON(ЗаписьJSON,  ПараметрыЗапроса.ТелоЗапроса);
		
		ТелоЗапросаJSON = ЗаписьJSON.Закрыть();
		
		HTTPЗапрос.УстановитьТелоИзСтроки(ТелоЗапросаJSON, Кодировка);
		
	КонецЕсли;
	
	HTTPОтвет = HTTPСоединение.ВызватьHTTPМетод(Тип, HTTPЗапрос);
	
	РезультатВыполнения = Новый Структура;
	РезультатВыполнения.Вставить("Ошибка", Ложь);
	РезультатВыполнения.Вставить("Ответ", Неопределено);
	
	Если Не (HTTPОтвет.КодСостояния = 200 Или HTTPОтвет.КодСостояния = 201) Тогда
		РезультатВыполнения.Ошибка = Истина;
	КонецЕсли;
	
	РезультатJSON = HTTPОтвет.ПолучитьТелоКакСтроку(Кодировка);
	
	ЧтениеJSON = Новый ЧтениеJSON;
	ЧтениеJSON.УстановитьСтроку(РезультатJSON);
	
	СтруктураHTTPОтвета = ПрочитатьJSON(ЧтениеJSON);
	
	ЧтениеJSON.Закрыть();
	
	РезультатВыполнения.Ответ = СтруктураHTTPОтвета;
		
	Возврат РезультатВыполнения;
	
КонецФункции

Функция HTTPСоединениеOzon()
	
	АдресРесурса = "api-seller.ozon.ru";
	
	Порт = 443;
	
	Таймаут = 10;
	
	SSL = Новый ЗащищенноеСоединениеOpenSSL;
	
	HTTPСоединение = Новый HTTPСоединение(АдресРесурса, Порт,,,, Таймаут, SSL);
	
	Возврат HTTPСоединение;
	
КонецФункции

#КонецОбласти

#КонецЕсли