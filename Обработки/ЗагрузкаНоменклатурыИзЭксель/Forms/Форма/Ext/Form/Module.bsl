
#Область ОбработчикиСобытийФормы
// Код процедур и функций
#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы
// Код процедур и функций
#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КаталогНачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	
	Диалог = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	Диалог.Заголовок = "Выберите файл";
	Диалог.ПолноеИмяФайла = "";
	Фильтр = "xlsx (*.xlsx)|*.xlsx";
	Диалог.Фильтр = Фильтр;
	Диалог.МножественныйВыбор = Ложь;
	
	Оповещение = Новый ОписаниеОповещения("ОткрытьФайлЗавершение", ЭтотОбъект);
	Диалог.Показать(Оповещение);
	
КонецПроцедуры

&НаКлиенте
Процедура ПрочитатьФайл(Команда)
	
	Если ПустаяСтрока(Каталог) Тогда
		ТекстСообщения = Нстр("ru = 'Не выбран файл в каталоге, выберите файл и попробуйте снова'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения,, "Каталог");
		Возврат;
	КонецЕсли;
	
	Файл = Новый Файл(Каталог);
	Если Не Файл.Существует() Тогда
		ТекстСообщения = Нстр("ru = 'Файл не существует, выберите новый файл и попробуйте снова'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения,, "Каталог");
		Возврат;
	КонецЕсли;
	
	Оповещение = Новый ОписаниеОповещения("ПрочитатьФайлЗавершение", ЭтотОбъект);
	НачатьПомещениеФайлаНаСервер(Оповещение,,, "", Каталог, УникальныйИдентификатор);
	
КонецПроцедуры

&НаКлиенте
Процедура ПеренестиВДокумент(Команда)
	
	Если Не Товары.Количество() Тогда
		ТекстСообщения = Нстр("ru = 'Табличная часть не заполнена, заполните таблицу и попробуйте снова'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьНастройки(Команда)
	
	ОткрытьФорму("ВнешняяОбработка.ЗагрузкаНоменклатурыИзЭксель.Форма.Настройки");
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ОткрытьФайлЗавершение(ВыбранныеФайлы, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(ВыбранныеФайлы) = Тип("Массив") И ВыбранныеФайлы.Количество() Тогда
		Каталог = ВыбранныеФайлы[0];
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПрочитатьФайлЗавершение(ОписаниеПомещенногоФайла, ДополнительныеПараметры) Экспорт
	
	ОбработатьФайл(ОписаниеПомещенногоФайла.Адрес, ОписаниеПомещенногоФайла.СсылкаНаФайл.Файл.Имя)
	
КонецПроцедуры

&НаСервере
Процедура ОбработатьФайл(Знач АдресФайлаВХранилище, Знач ИмяФайла)

	ДвоичныеДанные = ПолучитьИзВременногоХранилища(АдресФайлаВХранилище);
	
	ПутьКФайлу = СтрШаблон("%1%2", КаталогВременныхФайлов(), ИмяФайла);
	
	Попытка
		ДвоичныеДанные.Записать(ПутьКФайлу);
	Исключение
		ТекстИсключения = Нстр("ru = 'Не удалость произвести чтение файла. Обратитесь к администратору системы'");
		ВызватьИсключение(ТекстИсключения);
	КонецПопытки;
	
	Товары.Очистить();
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.Прочитать(ПутьКФайлу, СпособЧтенияЗначенийТабличногоДокумента.Значение);
	
	ТабЗначений = Новый ТаблицаЗначений;
	
	ПоследняяСтрока = ТабличныйДокумент.ВысотаТаблицы;
	ПоследняяКолонка = ТабличныйДокумент.ШиринаТаблицы;
	
	ОбработкаОбъект = РеквизитФормыВЗначение("Объект");
	Настройки = ОбработкаОбъект.ПолученныеНастройки();
	Если Настройки <> Неопределено Тогда
		НачалоСтрокиЧтения = Настройки.НачалоСтрокиЧтения;
		КолонкаАртикул = Настройки.КолонкаАртикул;
		КолонкаНоменклатура = Настройки.КолонкаНоменклатура;
		КолонкаКоличествоФакт = Настройки.КолонкаКоличествоФакт;
	Иначе
		НачалоСтрокиЧтения = 1;
		КолонкаАртикул = 1;
		КолонкаНоменклатура = 2;
		КолонкаКоличествоФакт = 3;
	КонецЕсли;

	Для НомерСтроки = НачалоСтрокиЧтения По ПоследняяСтрока Цикл
		ОбластьАртикул = ТабличныйДокумент.Область(НомерСтроки, КолонкаАртикул);
		ОбластьНоменклатура = ТабличныйДокумент.Область(НомерСтроки, КолонкаНоменклатура);
		ОбластьКоличествоФакт = ТабличныйДокумент.Область(НомерСтроки, КолонкаКоличествоФакт);
		
		Если ПустаяСтрока(ОбластьНоменклатура.Текст) Тогда
			Прервать;
		КонецЕсли;
		
		НайденнаяНоменклатура = Неопределено;
		Если ЗначениеЗаполнено(ОбластьАртикул.Текст) Тогда
			НайденнаяНоменклатура = Справочники.Номенклатура.НайтиПоКоду(ОбластьАртикул.Текст);
		Иначе
			НайденнаяНоменклатура = Справочники.Номенклатура.НайтиПоНаименованию(ОбластьНоменклатура.Текст);
		КонецЕсли;
		
		Если ЗначениеЗаполнено(НайденнаяНоменклатура) Тогда
			НоваяСтрока = Товары.Добавить();
			НоваяСтрока.Номенклатура = НайденнаяНоменклатура;
			Попытка
				НоваяСтрока.КоличествоФакт = Число(ОбластьКоличествоФакт.Текст);
			Исключение
				Продолжить;
			КонецПопытки;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти


