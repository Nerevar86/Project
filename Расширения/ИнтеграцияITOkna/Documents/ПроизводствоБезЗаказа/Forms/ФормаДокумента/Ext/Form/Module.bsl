
&НаКлиенте
Процедура IT_Okna_ПослеЗаписиПосле(ПараметрыЗаписи)
	Если Объект.ИдЗаказаITOkna <> 0 Тогда
		Оповестить("ОбновитьПроизводствоБезЗаказаITOkna");
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура IT_Okna_ПриСозданииНаСервереПеред(Отказ, СтандартнаяОбработка)
	
	Если Объект.ИдЗаказаITOkna > 0 Тогда
		
		НавигационнаяСсылкаНаЗаказ = НавигационнаяСсылкаНаЗаказ(Объект.ИдЗаказаITOkna);
		
		Если НавигационнаяСсылкаНаЗаказ <> Неопределено Тогда
			
			НовыйРеквизит = Новый РеквизитФормы("ITOkna_ЗаказКлиента", Новый ОписаниеТипов("ФорматированнаяСтрока"));
			
			ДобавляемыеРеквизиты = Новый Массив;
			ДобавляемыеРеквизиты.Добавить(НовыйРеквизит);
			
			ИзменитьРеквизиты(ДобавляемыеРеквизиты);
			
			НовыйЭлемент = Элементы.Добавить("ITOkna_ФормаЗаказКлиента", Тип("ПолеФормы"), Элементы.ГруппаОсновное);
			НовыйЭлемент.Заголовок = Нстр("ru = 'Заказ'");
			НовыйЭлемент.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Нет;
			НовыйЭлемент.Вид = ВидПоляФормы.ПолеНадписи;
			НовыйЭлемент.ПутьКДанным = "ITOkna_ЗаказКлиента";
			НовыйЭлемент.УстановитьДействие("ОбработкаНавигационнойСсылки", "ITOkna_ФормаЗаказКлиентаНажатие");
			
			ЭтотОбъект["ITOkna_ЗаказКлиента"] = Новый ФорматированнаяСтрока(
				НавигационнаяСсылкаНаЗаказ.Представление,,,, НавигационнаяСсылкаНаЗаказ.Ссылка);
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция НавигационнаяСсылкаНаЗаказ(ИдЗаказа)
	
	НавигационнаяСсылкаНаЗаказ = Неопределено;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
	|	ЗаказКлиента.Ссылка КАК Ссылка,
	|	ЗаказКлиента.Номер КАК Номер,
	|	ЗаказКлиента.Дата КАК Дата
	|ИЗ
	|	Документ.ЗаказКлиента КАК ЗаказКлиента
	|ГДЕ
	|	ЗаказКлиента.ИдЗаказаITOkna = &ИдЗаказа
	|
	|УПОРЯДОЧИТЬ ПО
	|	ЗаказКлиента.Дата УБЫВ";
	
	Запрос.УстановитьПараметр("ИдЗаказа", ИдЗаказа);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если Не РезультатЗапроса.Пустой() Тогда
		
		Выборка = РезультатЗапроса.Выбрать();
		
		Если Выборка.Следующий() Тогда
			
			Представление = СтрШаблон("Заказ № %1 от %2", СокрЛП(Формат(Выборка.Номер, "ЧГ=0")), СокрЛП(Формат(Выборка.Дата, "ДЛФ=DD")));
			СсылкаНаЗаказ = ПолучитьНавигационнуюСсылку(Выборка.Ссылка);
			
			НавигационнаяСсылкаНаЗаказ = Новый Структура;
			НавигационнаяСсылкаНаЗаказ.Вставить("Ссылка", СсылкаНаЗаказ);
			НавигационнаяСсылкаНаЗаказ.Вставить("Представление", Представление);
			
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат НавигационнаяСсылкаНаЗаказ;
	
КонецФункции

&НаКлиенте
Процедура ITOkna_ФормаЗаказКлиентаНажатие(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка);
	ПерейтиПоНавигационнойСсылке(НавигационнаяСсылкаФорматированнойСтроки);	
КонецПроцедуры

